plugins {
    id 'war'
    id 'io.openliberty.tools.gradle.Liberty' version '3.5.1'
    id 'groovy'
    id 'checkstyle'
}

version "${version}"
group "${group}"

sourceCompatibility = 17
targetCompatibility = 17
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

if(libertyInstallDir) {
    liberty {
        server {
            installDir = "${libertyInstallDir}"
        }
    }
}

checkstyle {
    configurations {
        configDirectory
    }
    maxWarnings = 0
    ignoreFailures false
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = true
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    /* Microprofile Core */
    providedCompile 'jakarta.platform:jakarta.jakartaee-api:9.1.0'
    providedCompile 'org.eclipse.microprofile:microprofile:5.0'

    /* Testing */
    testImplementation 'org.apache.groovy:groovy-all:4.0.6'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.12.13'
    testImplementation 'org.eclipse:yasson:3.0.2'
    testImplementation 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:3.0.3'
    testImplementation 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-inmemory:3.0.3'
    testImplementation 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:3.0.3'
    testImplementation 'org.glassfish.jersey.ext:jersey-bean-validation:3.0.3'
    testImplementation 'org.glassfish.jersey.inject:jersey-hk2:3.0.3'
    testImplementation 'org.glassfish.jersey.media:jersey-media-json-jackson:3.0.3'

    /* Logging */
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.2'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.2'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.17.2'

    /* HTML and CSV Libraries */
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.15.3'
    implementation group: 'com.opencsv', name: 'opencsv', version: '5.7.1'
}

clean.doFirst {
    delete 'src/main/liberty/config/server.env'
}

processResources {
    filesMatching('**/*.properties') {
        expand project.properties
    }
}

/* GitHooks config */
task installLocalGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}
build.dependsOn installLocalGitHook

clean.dependsOn 'libertyStop'